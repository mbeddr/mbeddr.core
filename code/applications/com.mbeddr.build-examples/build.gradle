// Dependency versions
ext.mpsVersion = '3.4.2'
ext.mbeddrVersion = '1.0.+'
// URL to mbeddr Nexus
ext.dependencyRepositories = [
    'https://artifacts.itemis.cloud/repository/maven-mps/'
]
// Location where resolved dependencies are stored
ext.artifactsDir = new File(rootDir, 'artifacts')
apply plugin: 'base'
// Configurations to which we link depedencies
configurations {
    mps
    mbeddrPlatformArtifacts
}
// Dependencies + versions
dependencies {
    mps "com.jetbrains:mps:$mpsVersion"
    mbeddrPlatformArtifacts "com.mbeddr:platform:$mbeddrVersion"
}
// Registration of artifact repositories
repositories {
    for (repoUrl in project.dependencyRepositories) {
        maven {
            url repoUrl
        }
    }
}
// Tasks for resolving dependencies (MPS + mbeddr platform)
task resolve_mps(type: Copy) {
    dependsOn configurations.mps
    from {
        configurations.mps.resolve().collect { zipTree(it) }
    }
    into "$buildDir/mps"
}
task resolve_mbeddr_platform(type: Copy) {
    from {
        configurations.mbeddrPlatformArtifacts.resolve().collect { zipTree(it) }
    }
    into artifactsDir
}
// Ant properties specifying file system paths
ant.properties['mps.home'] = resolve_mps.destinationDir
ant.properties['mbeddr.github.core.home'] = rootDir
ant.properties['build.dir'] = rootDir
ant.properties['artifacts.root'] = resolve_mbeddr_platform.destinationDir
ant.importBuild('build.xml') { target -> 'ant-' + target }
// Declaring task dependencies to resolve artifact dependencies before building the MPS project
tasks['ant-build'].dependsOn resolve_mps, resolve_mbeddr_platform