import de.itemis.mps.gradle.GitBasedVersioning

ext.releaseRepository = 'https://projects.itemis.de/nexus/content/repositories/mbeddr'
ext.snapshotRepository = 'https://projects.itemis.de/nexus/content/repositories/mbeddr_snapshots'

//MPS versions
ext.mpsMajor = "2018.1"
ext.mpsBuild = "2018.1"

ext.artifactsDir = new File(rootDir, 'artifacts')

ext.ciBuild = project.findProperty("forceCI") ?: !project.findProperty('mpsHomeDir') && project.findProperty("teamcity")

buildscript {
    repositories {
        mavenLocal()
        maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }
    }

    dependencies {
        classpath 'de.itemis.mps:mps-gradle-plugin:1.0.22.+'
    }
}

subprojects {
    // required plugins
    apply plugin: "base"
    apply plugin: "maven-publish"
    ext.mbeddrMajor = "1"
    ext.mbeddrMinor = "0"

    if (project.hasProperty("mbeddrVersion")) {
        ext.mbeddrBuildNumber = project.getProperty('mbeddrVersion')
    } else {
        // locally versions are SNAPSHOT
        if (ciBuild) {
            // setting mbeddrMajor
            if (project.hasProperty('mbeddrMajor')) {
                ext.mbeddrMajor = project.getProperty('mbeddrMajor')
            }

            // setting mbeddrMinor
            if (project.hasProperty('mbeddrMinor')) {
                ext.mbeddrMinor = project.getProperty('mbeddrMinor')
            }

            // setting mbeddrBuild
            if (project.hasProperty('mbeddrBuild')) {
                ext.mbeddrBuild = project.getProperty('mbeddrBuild')
            } else {
                ext.mbeddrBuild = GitBasedVersioning.getGitBranch()
            }

            if (project.hasProperty("mbeddrBuildCounter")) {
                ext.mbeddrBuildCounter = project.getProperty("mbeddrBuildCounter")
            } else {
                ext.mbeddrBuildCounter = GitBasedVersioning.getGitCommitCount()
            }
            if(mbeddrBuild == "stable") {
                mbeddrBuild = "master"
            }
            ext.mbeddrBuildNumber = GitBasedVersioning.getVersion(mbeddrBuild, mbeddrMajor, mbeddrMinor, mbeddrBuildCounter)
            println "##teamcity[buildNumber '${mbeddrBuildNumber}']"
        } else {
            println "Local build detected. Build will be a SNAPSHOT."
            ext.mbeddrBuildNumber = "1.0-SNAPSHOT"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion '4.4.1'
    distributionType 'all'
}
