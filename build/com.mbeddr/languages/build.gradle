// :build:com.mbeddr.build
def script_build_mbeddr = new File(scriptsBasePath + "/com.mbeddr.build/" + "build.xml")

ant.taskdef(name: "makeTests",
        classname: "com.mbeddr.tools.ant.MakeTestsTask",
        classpath: file(rootProject.projectDir.absolutePath + "/tools/ant/tasks/TeamcityMakeTests.jar")
)

// Ant <junit> task support
repositories {
    mavenCentral()
}
configurations {
    junitAnt
    mpsArtifacts
}

ext.dependencyRepositories = [
        'https://projects.itemis.de/nexus/content/repositories/mbeddr',
        'https://projects.itemis.de/nexus/content/repositories/mbeddr_snapshots' ]

dependencies {
    junitAnt 'junit:junit:4.12'
    junitAnt('org.apache.ant:ant-junit:1.9.7') {
        transitive = false
    }
    junitAnt('org.apache.ant:ant-junit4:1.9.7') {
        transitive = false
    }
    mpsArtifacts "com.mbeddr:platform:$mbeddrBuildNumber"
}


task resolveMpsArtifacts() {
    doLast {
        copy {
            from {
                    configurations.mpsArtifacts.resolve().collect { zipTree(it) }
                 }
            into artifactsDir
        }
    }
}



//move spawner into mbeddr script
task build_spawner(type: BuildLanguages, dependsOn: ':build:com.mbeddr:platform:copy_allScripts') {
    script scriptFile('com.mbeddr.build/spawner.xml')
}

task copy_spawner(type: Copy, dependsOn: build_spawner) {
    from "$rootDir/artifacts/spawner/"
    into "$mpsHomeDir/plugins"
}

task install_spawner(type: Copy, dependsOn: build_spawner) {
    from "$rootDir/artifacts/spawner/"
    include "mbeddr.spawner/"
    into "$mpsInstallationDir/plugins"
}

task install(dependsOn: [install_spawner, ':build:com.mbeddr:platform:install_sl_all_nativelibs']) {}

gradle.taskGraph.whenReady {taskGraph ->
    if (taskGraph.hasTask(install)) {
        if (!project.hasProperty("MPS_Installation")) {
            print("Missing MPS_Installation property please run gradle with -PMPS_Installation=<path to you local MPS>")
            throw new StopExecutionException("Missing MPS_Installation property please run gradle with -PMPS_Installation=<path to you local MPS>")
        }
    }
}

ant.taskdef(name: 'junit', classname: 'org.apache.tools.ant.taskdefs.optional.junit.JUnitTask',
        classpath: configurations.junitAnt.asPath)
ant.taskdef(name: 'junitreport', classname: 'org.apache.tools.ant.taskdefs.optional.junit.XMLResultAggregator',
        classpath: configurations.junitAnt.asPath)

def mbeddrDependencies

//on teamcity we don't build the platform we take it from the nexus. Locally we want to build it
if (ciBuild) {
    mbeddrDependencies = [copy_spawner, resolveMpsArtifacts]
} else {
    mbeddrDependencies = [copy_spawner, ':build:com.mbeddr:platform:build_platform']
}

task build_mbeddr(type: BuildLanguages, dependsOn: mbeddrDependencies) {
    script script_build_mbeddr
}

def script_test_mbeddrTutorial = new File(scriptsBasePath + "/com.mbeddr.tutorial/" + "build-tests.xml")

def script_test_mbeddrPerformance = new File(scriptsBasePath + "/com.mbeddr.core/" + "build-performance-tests.xml")
def script_test_mbeddrDebugger = new File(scriptsBasePath + "/com.mbeddr.debugger/" + "build-tests.xml")
def script_test_mbeddrCoreEx = new File(scriptsBasePath + "/com.mbeddr.core/" + "build-ex-tests.xml")
def script_test_mbeddrCoreExLibrary = new File(scriptsBasePath + "/com.mbeddr.core/" + "build-aLibrary.xml")
def script_test_mbeddrCoreTs = new File(scriptsBasePath + "/com.mbeddr.core/" + "build-ts-tests.xml")
def script_test_mbeddrCcTs = new File(scriptsBasePath + "/com.mbeddr.cc/" + "build-ts-tests.xml")
def script_test_mbeddrCcEx = new File(scriptsBasePath + "/com.mbeddr.cc/" + "build-ex-tests.xml")
def script_test_mbeddrExtTs = new File(scriptsBasePath + "/com.mbeddr.ext/" + "build-ts-tests.xml")
def script_test_mbeddrExtEx = new File(scriptsBasePath + "/com.mbeddr.ext/" + "build-ex-tests.xml")


def script_build_tutorialLanguages = new File(scriptsBasePath + "/com.mbeddr.tutorial/" + "build-languages.xml")
def script_build_tutorialSolutions = new File(scriptsBasePath + "/com.mbeddr.tutorial/" + "build-solutions.xml")

task build_tutorial_languages(type: BuildLanguages, dependsOn: build_mbeddr) {
    script script_build_tutorialLanguages
}

task build_tutorial_solutions(type: BuildLanguages, dependsOn: build_tutorial_languages) {
    script script_build_tutorialSolutions
}

task build_tutorial(dependsOn: build_tutorial_solutions) { }


task test_mbeddr_tutorial(type: TestLanguages, dependsOn: build_tutorial) {
    script script_test_mbeddrTutorial
}
task test_mbeddr_ext_ts(type: TestLanguages, dependsOn: build_mbeddr) {
    script script_test_mbeddrExtTs
}
task test_mbeddr_performance(type: TestLanguages, dependsOn: build_mbeddr) {
    script script_test_mbeddrPerformance
}

task test_mbeddr_debugger(type: TestLanguages, dependsOn: build_mbeddr) {
    script script_test_mbeddrDebugger
}

task build_mbeddr_core_ex(type: BuildLanguages, dependsOn: build_mbeddr) {
    script script_test_mbeddrCoreEx
}

task build_mbeddr_core_ex_library(type: BuildLanguages, dependsOn: build_mbeddr) {
    script script_test_mbeddrCoreExLibrary
}

task test_mbeddr_core_ts(type: TestLanguages, dependsOn: build_mbeddr) {
    script script_test_mbeddrCoreTs
}

task test_mbeddr_core_ex_library << {
    ant.makeTests(workingDirectory: rootProject.projectDir.absolutePath + "/code/languages/com.mbeddr.core/tests/aLibrary/source_gen/aLibrary/lib")
}

task test_mbeddr_core_ex << {
    ant.makeTests(workingDirectory: rootProject.projectDir.absolutePath + "/code/languages/com.mbeddr.core")
}

task test_mbeddr_core(dependsOn: [test_mbeddr_core_ex, test_mbeddr_core_ts, test_mbeddr_core_ex_library]) {

}
test_mbeddr_core_ex_library.dependsOn build_mbeddr_core_ex
test_mbeddr_core_ex.dependsOn test_mbeddr_core_ex_library
test_mbeddr_core_ex_library.dependsOn build_mbeddr_core_ex_library

task test_mbeddr_cc_ex << {
    ant.makeTests(workingDirectory: rootProject.projectDir.absolutePath + "/code/languages/com.mbeddr.cc")
}

task build_mbeddr_cc_ex(type: RunMbeddrAntScript, dependsOn: build_mbeddr) {
    script script_test_mbeddrCcEx
    targets 'clean', 'generate'
}

task test_mbeddr_cc_ts(type: TestLanguages, dependsOn: build_mbeddr) {
    script script_test_mbeddrCcTs
}

task test_mbeddr_cc(dependsOn: [build_mbeddr_cc_ex, test_mbeddr_cc_ex, test_mbeddr_cc_ts]) {}

task build_mbeddr_ext_ex(type: BuildLanguages, dependsOn: build_mbeddr) {
    script script_test_mbeddrExtEx
}

task test_mbeddr_ext_ex << {
    ant.makeTests(workingDirectory: rootProject.projectDir.absolutePath + "/code/languages/com.mbeddr.ext")
}



task test_mbeddr_ext(dependsOn: [build_mbeddr_ext_ex, test_mbeddr_ext_ex, test_mbeddr_ext_ts]) {}


task test_mbeddr(dependsOn: [test_mbeddr_core, test_mbeddr_performance, test_mbeddr_cc, test_mbeddr_ext]) {}

def pubDeb
if (ciBuild) {
    pubDeb = test_mbeddr
} else {
    pubDeb = build_mbeddr
}

task publish_mbeddr(type: Zip, dependsOn: pubDeb) {
    archiveName 'com.mbeddr.zip'
    from artifactsDir
    include "mbeddr/**"
}

publishing {
    publications {
        mbeddr(MavenPublication) {
            groupId 'com.mbeddr'
            artifactId 'mbeddr'
            version mbeddrBuildNumber
            artifact(publish_mbeddr) {}
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.mpsArtifacts.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

rootProject.defaultTasks 'build_mbeddr'

task publishMbeddrToLocal (dependsOn: ['publishMbeddrPublicationToMavenLocal',
':build:com.mbeddr:platform:publishMbeddrPlatformToLocal']) {}

check.dependsOn test_mbeddr