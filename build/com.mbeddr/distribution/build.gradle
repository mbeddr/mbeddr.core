
configurations {
    junitAnt
    mpsArtifacts
    cbmcWin {
        description = 'cbmc windows'
    }
    jreMac
    jreWin
}

ext.dependencyRepositories = [
        'https://projects.itemis.de/nexus/content/repositories/mbeddr',
        'https://projects.itemis.de/nexus/content/repositories/mbeddr_snapshots' ]

dependencies {
    mpsArtifacts "com.mbeddr:mbeddr:$mbeddrBuildNumber"
    jreMac "com.jetbrains.jdk:jdk:8u112b408.4:osx_x64@tgz"
    jreMac "com.jetbrains.jdk:jdk:8u112b408.4:windows_x64@tgz"
}

task resolveMpsArtifacts() {
    doLast {
        copy {
            from {
                    configurations.mpsArtifacts.resolve().collect { zipTree(it) }
                 }
            into artifactsDir
        }
    }
}





def script_build_mbeddrAllInOne = new File(scriptsBasePath + "/com.mbeddr.allInOne/" + "build.xml")

task build_all_in_one(type: BuildLanguages, dependsOn: [':build:com.mbeddr:platform:copy_allScripts', resolveMpsArtifacts]) {
    script script_build_mbeddrAllInOne
}

task build_platform_distribution(type: BuildLanguages, dependsOn: [':build:com.mbeddr:platform:copy_allScripts', resolveMpsArtifacts]) {
    script scriptFile('com.mbeddr.platform/build-distribution.xml')
    // Support incremental build
    inputs.file(script)
}

task package_tutorial(type: Zip) {
    from rootProject.projectDir.absolutePath + "/code/applications/tutorial"
    destinationDir  new File(rootProject.projectDir.absolutePath + "/artifacts/" + "com.mbeddr.tutorial")
    archiveName "com.mbeddr.tutorial.zip"
}

task prepare_distribution(dependsOn: [build_all_in_one, build_platform_distribution, package_tutorial])


def script_build_mbeddr_branding = new File(scriptsBasePath + "/com.mbeddr.rcp/" + "mps.xml")
def script_build_mbeddrRCP = new File(scriptsBasePath + "/com.mbeddr.rcp/" + "mpsDistribution.xml")

task build_mbeddr_branding(type: BuildLanguages, dependsOn: prepare_distribution) {
    script script_build_mbeddr_branding
}

task build_mbeddrRCP(type: RunMbeddrAntScript, dependsOn: build_mbeddr_branding) {
    script script_build_mbeddrRCP
    targets 'clean', 'assemble'
}

def installer_repo = project.hasProperty('InstallerRepo') ? project.getProperty('InstallerRepo') : rootProject.projectDir.absolutePath + '/../mbeddr.installer.win'

task prepare_installer(type: Copy, dependsOn: build_mbeddrRCP) {
    from {
        zipTree(rootProject.projectDir.absolutePath + "/artifacts/mpsDistribution/" + "mbeddr-win.zip")
    }
    into installer_repo + "/files/mbeddr/"
}

task resolve_cbmcWinForInstaller(type: Copy)  {
    from {
        configurations.cbmcWin.collect { zipTree(it) }
    }
    into rootProject.projectDir.absolutePath + "/artifacts/cbmc"
}

task resolve_jreWindows(type: Copy)  {
    from {
        configurations.cbmcWin.collect { tarTree(it) }
    }
    into installer_repo + "/files/mbeddr/"
}

task unzip_cbmcWinZipForInstaller(type: Copy, dependsOn: resolve_cbmcWinForInstaller)  {
    from zipTree(rootProject.projectDir.absolutePath + "/artifacts/cbmc/"+ "cbmc-win.zip")
    into file(installer_repo +"/files/3rd-party/cbmc")
}

task build_installer(type:Exec, dependsOn: [prepare_installer, unzip_cbmcWinZipForInstaller, resolve_jreWindows]) {
  workingDir installer_repo

  //on windows:
  commandLine 'cmd', '/c', "NSIS\\makensis.exe", installer_repo + "\\mbeddr.nsi"

  //store the output instead of printing to the console:
  standardOutput = new ByteArrayOutputStream()

  //extension method stopTomcat.output() can be used to obtain the output:
  ext.output = {
    return standardOutput.toString()
  }
}

def script_build_dmg = new File(rootDir , "/buildUtil/build.xml")

task resolveJreMac(type: Copy) {
    from {
        configurations.jreMac.resolve()
    }
    into rootProject.projectDir.absolutePath + "/buildUtil"
    rename("jdk-8u112b408.4-osx_x64.tgz", "jdk.tar.gz")
}


task prepare_dmg(type:Copy, dependsOn: [build_mbeddrRCP, resolveJreMac]) {
    from rootProject.projectDir.absolutePath + "/artifacts/mpsDistribution/"
    into rootProject.projectDir.absolutePath + "/buildUtil"
}


task build_dmg(type: RunMbeddrAntScript, dependsOn: prepare_dmg) {
    script script_build_dmg
    targets 'macbuild'
}


