// path variables
// If mpsHomeDir is set explicitly, skip the MPS resolution step and use the explicit path (which may be relative from
// the root directory).
ext.skipresolve_mps = project.hasProperty('mpsHomeDir')
ext.mpsHomeDir = rootProject.file(findNonEmptyProperty('mpsHomeDir') ?: "MPS/MPS-mbeddr-$mpsBuild/")

def userHome = System.properties['user.home']
def mpsPluginsDirPattern
def os_name = System.properties['os.name'].toLowerCase()
// calculate system-specific plugins directory (since MPS 2020.1) according to:
// https://www.jetbrains.com/help/idea/tuning-the-ide.html?_ga=2.229302868.95678467.1595072035-1152171669.1521775253#plugins-directory
if (os_name.contains('mac')) {
    mpsPluginsDirPattern = "$userHome/Library/Application Support/JetBrains/%s/plugins"
} else if (os_name.contains('win')) {
    mpsPluginsDirPattern = "${System.getenv('APPDATA')}\\JetBrains\\%s\\plugins"
} else {
    mpsPluginsDirPattern = "$userHome/.local/share/JetBrains/%s"
}

if (project.hasProperty("MPS_PATHS_SELECTOR")) {
    ext.mpsPluginsDir = sprintf(mpsPluginsDirPattern, project.getProperty("MPS_PATHS_SELECTOR"))
} else {
    ext.mpsPluginsDir = sprintf(mpsPluginsDirPattern, "MPS$mpsMajor")
}

task install() {
    // dependencies to this task are added dynamically from other scripts (com.mbeddr.platform, com.mbeddr.languages)
    description "Install the required plugins into the MPS plugin repository"
    group "Build Setup"
    doFirst {
        // check parent gradle file for definition of the variables
        println "Installing required mbeddr plugins to '$mpsPluginsDir'"
        if (!project.hasProperty("MPS_PATHS_SELECTOR")) {
            println "To change 'MPS<...>' part, pass MPS_PATHS_SELECTOR property to gradle with -PMPS_PATHS_SELECTOR=<custom path selector>"
            println "The path selector only contains the actual MPS version, for instance \"MPS2017.3\", not the full path to the user plugin directory."
        }
    }
}

logger.info "skipresolve_mps: {}, mpsHomeDir: {}", ext.skipresolve_mps, ext.mpsHomeDir

// JDK_HOME required for adding tools.jar into classpath of the forked ant process
if (!hasProperty("jdk_home")) {
    def java_home = System.properties['java.home']
    def jdk_home = java_home
    if (!file("$jdk_home/lib").exists()) {
        throw new GradleException("Was not able to locate jdk home folder. Use 'jdk_home' project variable to specify JDK location explicitly. Current JAVA_HOME is: $java_home")
    }
    ext.jdk_home = jdk_home
}

logger.info "jdk_home: $jdk_home"

ext.artifactsDir = file(rootProject.projectDir.absolutePath + "/artifacts")

ext.mps_home = '-Dmps.home=' + mpsHomeDir.getAbsolutePath()
ext.build_dir = '-Dbuild.dir=' + file(rootProject.projectDir.absolutePath).getAbsolutePath()
ext.artifacts_root = '-Dartifacts.root=' + file(rootProject.projectDir.absolutePath + "/artifacts").getAbsolutePath()
String buildVersion = '-Dbuild=' + ext.mbeddrBuildNumber
String majorVersion = '-Dmajor.version=' + ext.mbeddrMajor
String minorVersion = '-Dminor.version=' + ext.mbeddrMinor
ext.mbeddr_home = ['-Dmbeddr.github.core.home=' + file(rootProject.projectDir.absolutePath).getAbsolutePath(), minorVersion, majorVersion, buildVersion]
ext.slall_home = ['-Dsl-all.home=' + file(artifactsDir.absolutePath + '/de.itemis.mps.extensions').getAbsolutePath(), minorVersion, majorVersion, buildVersion]
ext.dependsOnMPS_scriptArgs = [mps_home, build_dir, artifacts_root]
ext["itemis.mps.gradle.ant.defaultScriptArgs"] = ext.dependsOnMbeddr_scriptArgs = [*dependsOnMPS_scriptArgs, *mbeddr_home, *slall_home, "-Dmps.generator.skipUnmodifiedModels=true"]

// path locations
ext.mbeddrScripts_basePath = file(ant.properties['mbeddr.github.core.home'] + "/build").getAbsolutePath()
ext.allScripts_buildFileLocation = file(mbeddrScripts_basePath + "/" + "com.mbeddr.allScripts").getAbsolutePath()
ext.slAll_buildFileLocation = file(ant.properties['mbeddr.github.core.home'] + "/scripts" + "/" + "sl-all").getAbsolutePath()

// ant script locations
ext.scriptsBasePath = rootProject.file("scripts").absolutePath

ant.taskdef(name: "makeTests",
        classname: "com.mbeddr.tools.ant.MakeTestsTask",
        classpath: file(rootProject.projectDir.absolutePath + "/tools/ant/tasks/TeamcityMakeTests.jar")
)

// mbeddr properties
subprojects {
    // repository urls
    configureRepositories(it)

    // configs
    configurations {
        mps
        mpsExtensions
        ant_lib
    }

    dependencies {
        mps "com.jetbrains:mps:$mpsBuild"
        mpsExtensions "de.itemis.mps:extensions:$mpsExtensionsVersion"
        ant_lib "org.apache.ant:ant-junit:1.10.1"
    }

    ext["itemis.mps.gradle.ant.defaultScriptClasspath"] = configurations.ant_lib
}


File scriptFile(String relativePath) {
    new File("$rootDir/scripts/$relativePath")
}

private static void configureRepositories(Project project) {
    project.publishing {
        repositories {
            maven {
                url project.mbeddrBuildNumber.endsWith('-SNAPSHOT') ? project.snapshotRepository : project.releaseRepository
                if (project.hasProperty('nexusUsername')) {
                    credentials {
                        username project.nexusUsername
                        password project.nexusPassword
                    }
                }
            }

            //mbeddr build is "master" also for maintenance branches
            //using the clouse to delay evaluate from configuration to execution phase is important because the
            //mbeddrBuild property is created by a "subproject" block which is executed after this script is configured.
            //if no closure is used the build script won't compile.
            if({project.mbeddrBuild}() == "master") {
                maven {
                    name = "GitHubPackages"
                    url = "https://maven.pkg.github.com/mbeddr/mbeddr.core"
                    if (project.hasProperty("gpr.token")) {
                        credentials {
                            username = project.findProperty("gpr.user")
                            password = project.findProperty("gpr.token")
                        }
                    }
                }
            }

        }
    }
    project.repositories {
        mavenLocal()
        for (repoUrl in (project.dependencyRepositories + [project.snapshotRepository, project.releaseRepository]).unique()) {
            maven {
                url repoUrl
                if (project.hasProperty('nexusUsername')) {
                    credentials {
                        username project.nexusUsername
                        password project.nexusPassword
                    }
                }
            }
        }
        mavenCentral()
    }
}


task printVersions {
    doLast {
        println "mbeddrBuildNumber: $project.mbeddrBuildNumber"
		println "mbeddrPlatformBuildNumber: $project.mbeddrPlatformBuildNumber"
    }
}

task printRepositories {
    doLast {
        println "snapshotRepository: $snapshotRepository"
        println "releaseRepository: $releaseRepository"
        println "dependencyRepositories: $dependencyRepositories"
    }
}
